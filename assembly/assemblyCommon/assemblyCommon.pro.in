ARCHITECTURE=@architecture@
USEFAKEDEVICES="X@usefakedevices@"

LIBS += -L@basepath@/devices/lib -lTkModLabLang
LIBS += -L@basepath@/common -lCommon

equals(USEFAKEDEVICES,"X0") {
LIBS += -lueye_api
}

macx {
	CONFIG+=x86_64
	QMAKE_CXXFLAGS += -stdlib=libc++
	LIBS += -framework AppKit
	LIBS += -framework QuartzCore
	LIBS += -framework QTKit
	LIBS += -framework Cocoa
	LIBS += -L/opt/local/lib -ltiff
	QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.9
}

CONFIG += link_pkgconfig
PKGCONFIG += opencv

QMAKE = @qmake@

QMAKE_CXXFLAGS += -std=c++11

macx {
	QMAKE_CXXFLAGS += -DAPPLICATIONVERSIONSTR=\\\"unknown\\\"
}
else {
	QMAKE_CXXFLAGS += -DAPPLICATIONVERSIONSTR=\\\"`git describe --dirty --always --tags`\\\"
}

DEFINES += @configdefines@

QT += core gui xml network script svg
greaterThan(QT_MAJOR_VERSION, 4) {
  QT += widgets
} 

TEMPLATE = lib
TARGET = AssemblyCommon
DEPENDPATH += @basepath@/common
INCLUDEPATH += .
INCLUDEPATH += ..
INCLUDEPATH += @basepath@
INCLUDEPATH += @basepath@/common

# Input
HEADERS += AssemblyVUEyeCamera.h \
           AssemblyVUEyeModel.h \
           AssemblyUEyeCameraThread.h \
           AssemblyUEyeWidget.h \
           AssemblyUEyeCameraWidget.h \
           AssemblyUEyeView.h \
           AssemblyUEyeSnapShooter.h \
           AssemblyMarkerFinderThread.h \
           AssemblyVMarkerFinder.h \
           AssemblySensorMarkerFinder.h

equals(USEFAKEDEVICES,"X0") {
HEADERS += AssemblyUEyeCamera.h \
           AssemblyUEyeModel.h
} else {
HEADERS += AssemblyUEyeFakeCamera.h \
           AssemblyUEyeFakeModel.h
}

SOURCES += AssemblyVUEyeCamera.cc \
           AssemblyVUEyeModel.cc \
           AssemblyUEyeCameraThread.cc \
           AssemblyUEyeWidget.cc \
           AssemblyUEyeCameraWidget.cc \
           AssemblyUEyeView.cc \
           AssemblyUEyeSnapShooter.cc \
           AssemblyMarkerFinderThread.cc \
           AssemblyVMarkerFinder.cc \
           AssemblySensorMarkerFinder.cc

equals(USEFAKEDEVICES,"X0") {
SOURCES += AssemblyUEyeCamera.cc \
           AssemblyUEyeModel.cc
} else {
SOURCES += AssemblyUEyeFakeCamera.cc \
           AssemblyUEyeFakeModel.cc
}

ARCHITECTURE=@architecture@
USEFAKEDEVICES="X@usefakedevices@"
USEUEYE="X1"

LIBS += -L@basepath@/devices/lib -lTkModLabLang -lTkModLabKeyence
LIBS += -L@basepath@/common -lCommon

equals(USEFAKEDEVICES,"X1") {
USEUEYE="X0"
}

macx {
  USEUEYE="X0"
  CONFIG+=x86_64
  QMAKE_CXXFLAGS += -stdlib=libc++
  #QMAKE_MAC_SDK = macosx10.11
  QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.11
  #LIBS += -framework AppKit
  #LIBS += -framework QuartzCore
  #LIBS += -framework QTKit
  #LIBS += -framework Cocoa
}

equals(USEUEYE,"X1") {
LIBS += -lueye_api
}

CONFIG += link_pkgconfig
CONFIG += qtestlib
PKGCONFIG += opencv

QMAKE = @qmake@

QMAKE_CXXFLAGS += -std=c++11

macx {
  QMAKE_CXXFLAGS += -DAPPLICATIONVERSIONSTR=\\\"unknown\\\"
}
else {
  QMAKE_CXXFLAGS += -DAPPLICATIONVERSIONSTR=\\\"`git describe --dirty --always --tags`\\\"
}

DEFINES += @configdefines@

equals(USEFAKEDEVICES,"X1") {
DEFINES += USE_FAKEIO
}
equals(USEUEYE,"X1") {
DEFINES += USE_UEYE
}

QT += core gui xml network script svg
greaterThan(QT_MAJOR_VERSION, 4) {
  QT += widgets
} 

TARGET = AssemblyCommon
TEMPLATE = lib

macx {
  QMAKE_POST_LINK = install_name_tool -change libCommon.1.dylib @basepath@/common/libCommon.1.dylib $(TARGET)
}

DEPENDPATH += @basepath@/common
INCLUDEPATH += .
INCLUDEPATH += ..
INCLUDEPATH += @basepath@
INCLUDEPATH += @basepath@/common

greaterThan(QT_MAJOR_VERSION, 4) {
  cache()
}

# Input
HEADERS += AssemblyVUEyeCamera.h \
           AssemblyVUEyeModel.h \
           AssemblyUEyeCameraThread.h \
           AssemblyUEyeWidget.h \
           AssemblyUEyeCameraWidget.h \
           AssemblyUEyeView.h \
           AssemblyUEyeSnapShooter.h \
           AssemblyMarkerFinderThread.h \
           AssemblyVMarkerFinder.h \
           AssemblySensorMarkerFinder.h \
           AssemblySensorMarkerFinderWidget.h \
           LStepExpressModel.h \
           LStepExpressSettings.h \
           LStepExpressMotion.h \
           LStepExpressMotionManager.h \
           LStepExpressMotionThread.h \
           LStepExpressWidget.h \
           LStepExpressJoystickWidget.h \
           LStepExpressSettingsWidget.h \
           LStepExpressMeasurement.h \
           LStepExpressMeasurementWidget.h \
           LStepExpressMeasurementTable.h \
           LStepExpressPositionWidget.h \
           LStepExpressStatusWindow.h \
           LaserModel.h \
           LaserWidget.h \
           LaserThread.h \
           LaserControlsWidget.h 

equals(USEUEYE,"X1") {
HEADERS += AssemblyUEyeCamera.h \
           AssemblyUEyeModel.h
} else {
HEADERS += AssemblyUEyeFakeCamera.h \
           AssemblyUEyeFakeModel.h
}

SOURCES += AssemblyVUEyeCamera.cc \
           AssemblyVUEyeModel.cc \
           AssemblyUEyeCameraThread.cc \
           AssemblyUEyeWidget.cc \
           AssemblyUEyeCameraWidget.cc \
           AssemblyUEyeView.cc \
           AssemblyUEyeSnapShooter.cc \
           AssemblyMarkerFinderThread.cc \
           AssemblyVMarkerFinder.cc \
           AssemblySensorMarkerFinder.cc \
           AssemblySensorMarkerFinderWidget.cc \
           LStepExpressModel.cc \
           LStepExpressSettings.cc \
           LStepExpressMotion.cc \
           LStepExpressMotionManager.cc \
           LStepExpressMotionThread.cc \
           LStepExpressWidget.cc \
           LStepExpressJoystickWidget.cc \
           LStepExpressSettingsWidget.cc \
           LStepExpressMeasurement.cc \
           LStepExpressMeasurementWidget.cc \
           LStepExpressMeasurementTable.cc \
           LStepExpressPositionWidget.cc \
           LStepExpressStatusWindow.cc \
           LaserModel.cc \
           LaserWidget.cc \
           LaserThread.cc \ 
           LaserControlsWidget.cc

equals(USEUEYE,"X1") {
SOURCES += AssemblyUEyeCamera.cc \
           AssemblyUEyeModel.cc
} else {
SOURCES += AssemblyUEyeFakeCamera.cc \
           AssemblyUEyeFakeModel.cc
}
